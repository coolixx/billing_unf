#Если не Вебклиент и Не ТонкийКлиент И НЕ ТолстыйКлиентУправляемоеПриложение Тогда
	
Процедура ВыполнитьБиллинг(НастройкаБиллинга) Экспорт
	Если НЕ НастройкаБиллинга.ПометкаУдаления Тогда
		НастройкаДнейОповещений = НастройкаБиллинга.Настройки.Получить().ДниОповещенияКонтрагентов;
		
		Шаблон = НастройкаБиллинга.ШаблонСообщения;
		УчетнаяЗапись = НастройкаБиллинга.УчетнаяЗапись;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ВТНастройкиДнейОповещения.День КАК День
			|ПОМЕСТИТЬ ВТНастройкиДнейОповещения
			|ИЗ
			|	&ВТНастройкиДнейОповещения КАК ВТНастройкиДнейОповещения
			|ГДЕ
			|	ВТНастройкиДнейОповещения.Пометка = ИСТИНА
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	BS_ЗарегистрированныеОбъектыБиллинга.Объект,
			|	РАЗНОСТЬДАТ(&ТекущаяДата, BS_ЗарегистрированныеОбъектыБиллинга.ДатаОстановки, День) КАК День
			|ИЗ
			|	ВТНастройкиДнейОповещения КАК ВТНастройкиДнейОповещения
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.BS_ЗарегистрированныеОбъектыБиллинга КАК BS_ЗарегистрированныеОбъектыБиллинга
			|		ПО ВТНастройкиДнейОповещения.День = РАЗНОСТЬДАТ(&ТекущаяДата, BS_ЗарегистрированныеОбъектыБиллинга.ДатаОстановки,
			|			День)
			|ГДЕ
			|	BS_ЗарегистрированныеОбъектыБиллинга.Период <= &ТекущаяДата";
		
		Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
		Запрос.УстановитьПараметр("ВТНастройкиДнейОповещения", НастройкаДнейОповещений);
				
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			
			Пока Выборка.Следующий() Цикл
				ДополнительныеПараметры = Новый Структура;
				ДополнительныеПараметры.Вставить("ПреобразовыватьHTMLДляФорматированногоДокумента", Истина);
				ДополнительныеПараметры.Вставить("УчетнаяЗапись", УчетнаяЗапись);
		
				ПараметрыСообщения = Новый Структура;
				ПараметрыСообщения.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
				ПараметрыСообщения.Вставить("Шаблон", Шаблон);
				ПараметрыСообщения.Вставить("Предмет", Выборка.Объект);
				
				ШаблоныСообщенийСлужебный.СформироватьСообщениеИОтправить(ПараметрыСообщения); 
			КонецЦикла;
		КонецЕсли;
		
		Токен = bs_РаботаСAPIВызовСервера.ПолучитьТокен(НастройкаБиллинга);

		МассивИдентификаторов = BS_БиллингВызовСервера.ПолучитьИдентификаторыПриложенийДляОстановки(Токен);
		Если МассивИдентификаторов.Количество() > 0 Тогда
			Метод = BS_ОбщегоНазначенияВызовСервераПовтИсп.МетодОстоновкиПриложения();
			Для каждого Идентификатор из МассивИдентификаторов Цикл
				МетодИдентификатора = СтрШаблон(Метод, Идентификатор);
				bs_РаботаСAPIВызовСервера.ВызватьМетодAPI(МетодИдентификатора, Токен,
											BS_ОбщегоНазначенияВызовСервераПовтИсп.HTTPМетоды().POST);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ЗагрузитьСервисыКонтрагента(НастройкаБиллинга, ИДКонтрагента = Неопределено) Экспорт

	Токен = bs_РаботаСAPIВызовСервера.ПолучитьТокен(НастройкаБиллинга);

	Если ИДКонтрагента <> Неопределено Тогда
		Метод = СтрШаблон(BS_ОбщегоНазначенияВызовСервераПовтИсп.МетодПолученияСпискаПриложенийКлиентаПоID(), ИДКонтрагента);
	Иначе
		Метод = BS_ОбщегоНазначенияВызовСервераПовтИсп.МетодПолученияСпискаПриложений();
	КонецЕсли;
	
	Результат = bs_РаботаСAPIВызовСервера.ВызватьМетодAPI(Метод, Токен,
											  BS_ОбщегоНазначенияВызовСервераПовтИсп.HTTPМетоды().GET);
	
	bs_ОбработкаРезультатаВызовСервера.ОбработатьСервисыКонтрагента(НастройкаБиллинга, Результат);
	
КонецПроцедуры

#КонецЕсли
